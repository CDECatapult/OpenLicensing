-PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
 PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
 PREFIX owl: <http://www.w3.org/2002/07/owl#>
 PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
 PREFIX id: <http://openpermissions.org/ns/id/>
 PREFIX odrl: <http://www.w3.org/ns/odrl/2/>
 PREFIX op: <http://openpermissions.org/ns/op/1.0/>

 CONSTRUCT { ?s ?p ?o } WHERE {
    $startid$ rdf:type odrl:Policy .	
    { SELECT DISTINCT ?s {
             $startid$ (
                         odrl:permission
                       |(odrl:permission/( odrl:duty
                                         |(odrl:duty/odrl:constraint)
                                         | odrl:constraint
                                         )
                        )
                       | odrl:prohibition
                       |(odrl:prohibition/odrl:constraint)
                       )? ?s .
             } 
    }
    ?s ?p ?o .		 
    FILTER ( LANG(?o)="" || LANG(?o)="en" ) .
}
